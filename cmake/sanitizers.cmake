add_library(sanitizer_address INTERFACE)
add_library(sanitizer::address ALIAS sanitizer_address)
target_compile_options(sanitizer_address INTERFACE -fsanitize=address -fno-omit-frame-pointer)
target_link_libraries(sanitizer_address INTERFACE -fsanitize=address)

add_library(sanitizer_undefined INTERFACE)
add_library(sanitizer::undefined ALIAS sanitizer_undefined)
target_compile_options(sanitizer_undefined INTERFACE -fsanitize=undefined)
target_link_libraries(sanitizer_undefined INTERFACE -fsanitize=undefined)

add_library(sanitizer_thread INTERFACE)
add_library(sanitizer::thread ALIAS sanitizer_thread)
target_compile_options(sanitizer_thread INTERFACE -fsanitize=thread)
target_link_libraries(sanitizer_thread INTERFACE -fsanitize=thread)

add_library(sanitizer_memory INTERFACE)
add_library(sanitizer::memory ALIAS sanitizer_memory)
target_compile_options(sanitizer_memory INTERFACE -fsanitize=memory -fPIE -pie)
target_link_libraries(sanitizer_memory INTERFACE -fsanitize=memory)

# Select sanitizer based on DS_SANITIZER option
if(NOT DS_SANITIZER STREQUAL "none")
  if(DS_SANITIZER STREQUAL "address")
    set(SELECTED_SANITIZER sanitizer::address)
    message(STATUS "Using Address Sanitizer")
  elseif(DS_SANITIZER STREQUAL "memory")
    set(SELECTED_SANITIZER sanitizer::memory)
    message(STATUS "Using Memory Sanitizer")
  elseif(DS_SANITIZER STREQUAL "thread")
    set(SELECTED_SANITIZER sanitizer::thread)
    message(STATUS "Using Thread Sanitizer")
  elseif(DS_SANITIZER STREQUAL "undefined")
    set(SELECTED_SANITIZER sanitizer::undefined)
    message(STATUS "Using Undefined Behavior Sanitizer")
  else()
    message(FATAL_ERROR "Unknown sanitizer: ${DS_SANITIZER}. Valid options: address, memory, thread, undefined, none")
  endif()
else()
  message(STATUS "Sanitizers disabled (DS_SANITIZER=none)")
endif()
